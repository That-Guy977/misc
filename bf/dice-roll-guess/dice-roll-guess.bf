Dice Roll Guess game

=intro=
++++ ++++ [
  > ++++ ++++ G
  > ++++ ++++ ++++ ++ u
  > ++++ ++++ ++++ e
  > ++++ ++++ ++++ ++ s
  > ++++ ++++ ++++ ++ s
  > ++++ =space=
  > ++++ #
  > ++++ =space=
  > ++++ + (
  > ++++ ++ 1
  > ++++ + =dash=
  > ++++ ++ 6
  > ++++ + }
  > ++++ =space=
  > ++++ ++++ B
  > ++++ ++++ ++++ +++ y
  > ++++ =space=
  > ++++ ++++ ++ T
  > ++++ ++++ ++++ + h
  > ++++ ++++ ++++ a
  > ++++ ++++ ++++ ++ t
  > ++++ ++++ +++ _
  > ++++ ++++ G
  > ++++ ++++ ++++ ++ u
  > ++++ ++++ ++++ +++ y
  > ++++ +++ 9
  > ++++ ++ 7
  > ++++ ++ 7
  <<<<< <<<<< <<<<< <<<<< <<<<< <<< -
]>+++++++>+++++>+++++>+++>+++>>+++>>>+>+++++>++++++>+>>++>+>>++++>>+>++++>+++++++>+++++++>+++++>+>+>+++++++>+++++++
[<]>[.>]>

=init=
+>>
++++ ++++ [
  > ++++ ++ 4 // chosen by fair dice roll guaranteed to be random
  >
  > + =newline=
  > ++++ ++++ G
  > ++++ ++++ ++++ ++ u
  > ++++ ++++ ++++ e
  > ++++ ++++ ++++ ++ s
  > ++++ ++++ ++++ ++ s
  > ++++ +++ :
  > ++++ =space=
  >
  > ++++ ++++ C
  > ++++ ++++ + I
  > ++++ ++++ ++++ + n
  > ++++ ++++ ++++ c
  >
  > ++++ ++++ ++++ + o
  > ++++ ++++ ++++ ++ r
  > ++++ ++++ ++++ ++ r
  > ++++ ++++ ++++ e
  > ++++ ++++ ++++ c
  > ++++ ++++ ++++ ++ t
  <<<<< <<<<< <<<<< <<<<< << -
]+>++++>>++>+++++++>+++++>+++++>+++>+++>++>>>+++>+>++++++>+++>>+++++++>++>++>+++++>+++>++++
[<]<[<]<[<]<<<<

=loop=
[
  >>>>>[.>]<[<]<<<
  ,
  >>[>+<-]>[<<<->>+>-]<<<
  [ if incorrect
    [-]>-
    >>>.[>]>>[.>]>[.>]<[<]<[<]<[<]<<
    <
  ]>[ if correct
    >>>.[>]>.[>]>[.>]<[<]<[<]<[<]<<
    <<->
  ]
  +<<
]
